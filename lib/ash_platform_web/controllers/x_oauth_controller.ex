defmodule AshPlatformWeb.XOAuthController do
  @moduledoc false
  use AshPlatformWeb, :controller

  alias AshPlatform.Accounts.{SessionAuthority, XOAuth}

  def create(conn, %{"role" => role}) do
    case XOAuth.begin(claim(conn), role) do
      {:ok, payload} -> conn |> no_store() |> json(payload)
      {:error, :x_oauth_disabled} -> error(conn, :service_unavailable, "x_oauth_disabled")
      {:error, :invalid_role} -> error(conn, :unprocessable_entity, "invalid_role")
      {:error, :stale_authority} -> error(conn, :conflict, "stale_authority")
      {:error, _reason} -> error(conn, :bad_gateway, "x_oauth_unavailable")
    end
  end

  def delete(conn, %{"role" => role}) do
    case XOAuth.disconnect(claim(conn), role) do
      {:ok, payload} -> conn |> no_store() |> json(Map.put(payload, :ok, true))
      {:error, :invalid_role} -> error(conn, :unprocessable_entity, "invalid_role")
      {:error, :stale_authority} -> error(conn, :conflict, "stale_authority")
      {:error, _reason} -> error(conn, :unprocessable_entity, "disconnect_failed")
    end
  end

  def callback(conn, params) do
    result =
      case XOAuth.callback(claim(conn), params) do
        {:ok, payload} -> Map.merge(payload, %{status: "connected"})
        {:error, _reason} -> %{role: nil, generation: nil, status: "failed"}
      end

    conn
    |> no_store()
    |> put_resp_header(
      "content-security-policy",
      "default-src 'none'; script-src 'unsafe-inline'; style-src 'unsafe-inline'"
    )
    |> put_root_layout(false)
    |> render(:callback,
      origin: XOAuth.origin(),
      status: result.status,
      role: result.role,
      generation: result.generation
    )
  end

  defp claim(conn), do: conn |> get_session() |> SessionAuthority.claim()

  defp no_store(conn), do: put_resp_header(conn, "cache-control", "no-store")

  defp error(conn, status, code),
    do: conn |> no_store() |> put_status(status) |> json(%{error: code})
end

defmodule AshPlatformWeb.XOAuthHTML do
  @moduledoc false

  use AshPlatformWeb, :html

  def callback(assigns) do
    ~H"""
    <!doctype html>
    <html lang="en">
      <head>
        <meta charset="utf-8" />
        <meta name="viewport" content="width=device-width" />
        <title>X connection</title>
      </head>
      <body style="font-family:system-ui;background:#111;color:#fff;padding:2rem">
        <main
          id="x-oauth-result"
          data-origin={@origin}
          data-status={@status}
          data-role={@role || ""}
          data-generation={@generation || ""}
        >
          <p>
            {if @status == "connected",
              do: "X account connected.",
              else: "X connection could not be completed."}
          </p>
          <p><a href="/settings" style="color:#ff5a1f">Return to Regents</a></p>
        </main>
        <script>
          (() => {
            const result = document.getElementById("x-oauth-result");
            const value = (key) => result.dataset[key] || null;
            const message = {
              source: "ash-x-oauth",
              status: value("status"),
              role: value("role"),
              generation: value("generation")
            };
            if (window.opener) window.opener.postMessage(message, result.dataset.origin);
          })();
        </script>
      </body>
    </html>
    """
  end
end
