defmodule AshPlatform.Autolaunch.LabBidChainClient do
  @moduledoc false

  @behaviour AshPlatform.Autolaunch.ChainClient

  alias AshPlatform.Autolaunch.{Lab, LabAbi, LabRpc}
  alias AshPlatform.WalletActions.{Abi, Address, Envelope}

  @max_tick_walk 256
  @max_uint256 Integer.pow(2, 256) - 1

  @impl true
  def snapshot(%{auction: auction, signer: signer, max_price_q96: max_price}) do
    with {:ok, auction} <- Address.normalize(auction),
         {:ok, config, block, opts} <- LabRpc.current([:regent, :permit2]),
         :ok <- LabRpc.ensure_contract(auction, block, opts),
         {:ok, currency} <- call_address(config, auction, "currency()", [], block, opts),
         true <- Address.equal?(currency, Lab.address!(config, :regent)),
         {:ok, regent_balance} <-
           LabRpc.uint(config, :regent, "balanceOf(address)", [signer], block, opts),
         {:ok, token_allowance} <-
           LabRpc.uint(
             config,
             :regent,
             "allowance(address,address)",
             [signer, Lab.address!(config, :permit2)],
             block,
             opts
           ),
         {:ok, permit2_words} <-
           LabRpc.words(
             config,
             :permit2,
             "allowance(address,address,address)",
             [signer, currency, auction],
             3,
             block,
             opts
           ),
         [permit2_amount, permit2_expiration, _nonce] <- permit2_words,
         {:ok, prev_tick_price_q96} <- predecessor(config, auction, max_price, block, opts) do
      {:ok,
       %{
         auction: auction,
         currency: currency,
         regent_balance: regent_balance,
         token_allowance: token_allowance,
         permit2_amount: permit2_amount,
         permit2_expiration: permit2_expiration,
         prev_tick_price_q96: prev_tick_price_q96,
         predecessor_source: "bounded local auction tick walk",
         block: block,
         permit2: Lab.address!(config, :permit2),
         lab_binding: Lab.binding(config, [:regent, :permit2])
       }}
    else
      false -> {:error, :auction_currency_is_not_regent}
      :error -> {:error, :invalid_chain_response}
      {:error, reason} -> {:error, reason}
      _other -> {:error, :invalid_chain_response}
    end
  end

  @impl true
  def verify(envelope, step, hash) do
    with true <-
           Envelope.valid_for_confirmation?(envelope,
             resource: "autolaunch_auction",
             chain_id: Lab.chain_id()
           ),
         true <- Lab.binding_matches?(envelope["metadata"]["lab"], [:regent, :permit2]),
         {:ok, config} <- Lab.current(),
         current <- current_step(envelope, step),
         {:ok, outcome} <- LabRpc.canonical_outcome(config, envelope, current, hash) do
      settled(outcome, envelope, step, config)
    else
      false -> {:error, :lab_config_changed}
      {:error, reason} -> {:error, reason}
    end
  end

  defp predecessor(config, auction, max_price, block, opts) when is_integer(max_price) do
    with {:ok, floor} <- call_uint(config, auction, "floorPrice()", [], block, opts),
         true <- floor < max_price do
      walk_ticks(config, auction, floor, max_price, block, opts, 0)
    else
      false -> {:error, :bid_preparation_unavailable}
      {:error, reason} -> {:error, reason}
    end
  end

  defp predecessor(_config, _auction, nil, _block, _opts), do: {:ok, 0}

  defp walk_ticks(_config, _auction, _current, _max_price, _block, _opts, @max_tick_walk),
    do: {:error, :bid_preparation_unavailable}

  defp walk_ticks(config, auction, current, max_price, block, opts, hops) do
    with {:ok, [next, _demand]} <-
           call_words(config, auction, "ticks(uint256)", [current], 2, block, opts) do
      cond do
        next == @max_uint256 -> {:ok, current}
        next >= max_price -> {:ok, current}
        next > current -> walk_ticks(config, auction, next, max_price, block, opts, hops + 1)
        true -> {:error, :bid_preparation_unavailable}
      end
    end
  end

  defp settled(:pending, _envelope, _step, _config), do: {:ok, %{outcome: :pending}}
  defp settled(:reverted, _envelope, _step, _config), do: {:ok, %{outcome: :reverted}}

  defp settled({:success, logs}, envelope, :token_approval, config) do
    step = current_step(envelope, :token_approval)
    amount = String.to_integer(step["amount"])

    with {:ok, block} <- LabRpc.block_from_logs(logs),
         true <-
           Abi.approval_recorded?(
             logs,
             Lab.address!(config, :regent),
             envelope["expected_signer"],
             Lab.address!(config, :permit2),
             amount
           ),
         opts <- LabRpc.opts(config),
         {:ok, allowance} <-
           LabRpc.uint(
             config,
             :regent,
             "allowance(address,address)",
             [envelope["expected_signer"], Lab.address!(config, :permit2)],
             block,
             opts
           ) do
      {:ok, %{outcome: if(allowance == amount, do: :confirmed, else: :unverified)}}
    else
      false -> {:ok, %{outcome: :unverified}}
      {:error, reason} -> {:error, reason}
    end
  end

  defp settled({:success, logs}, envelope, :permit2_approval, config) do
    step = current_step(envelope, :permit2_approval)

    with {:ok, block} <- LabRpc.block_from_logs(logs),
         opts <- LabRpc.opts(config),
         {:ok, words} <-
           LabRpc.words(
             config,
             :permit2,
             "allowance(address,address,address)",
             [
               envelope["expected_signer"],
               envelope["arguments"]["currency"],
               envelope["to"]
             ],
             3,
             block,
             opts
           ),
         [amount, expiration, _nonce] <- words do
      expected =
        {String.to_integer(step["amount"]), String.to_integer(step["expiration"])}

      {:ok, %{outcome: if({amount, expiration} == expected, do: :confirmed, else: :unverified)}}
    else
      {:error, reason} -> {:error, reason}
      _other -> {:error, :invalid_chain_response}
    end
  end

  defp settled({:success, logs}, envelope, :bid, config) do
    signature = "BidSubmitted(uint256,address,uint256,uint128)"

    with {:ok, block} <- LabRpc.block_from_logs(logs),
         {:ok, {[bid_id, owner_word], [price, amount]}} <-
           LabAbi.event_words(Lab.abi!(config, :auction), signature, logs, envelope["to"]),
         {:ok, owner} <- Abi.word_address(owner_word),
         true <- Address.equal?(owner, envelope["expected_signer"]),
         true <- price == integer(envelope, "max_price_q96"),
         true <- amount == integer(envelope, "amount_atomic"),
         opts <- LabRpc.opts(config),
         {:ok, clearing_price} <-
           call_uint(config, envelope["to"], "clearingPrice()", [], block, opts) do
      id = Integer.to_string(bid_id)

      {:ok,
       %{
         outcome: :confirmed,
         onchain_bid_id: id,
         result: %{
           "onchain_bid_id" => id,
           "current_clearing_price" =>
             AshPlatform.WalletActions.Rpc.format_units(clearing_price, 18),
           "local_block_hash" => block.hash
         }
       }}
    else
      false -> {:ok, %{outcome: :unverified}}
      :error -> {:ok, %{outcome: :unverified}}
      {:error, reason} -> {:error, reason}
    end
  end

  defp call_address(config, address, signature, arguments, block, opts) do
    AshPlatform.WalletActions.Rpc.call_address(
      address,
      LabAbi.encode(Lab.abi!(config, :auction), signature, arguments),
      block,
      opts
    )
  end

  defp call_uint(config, address, signature, arguments, block, opts),
    do: LabRpc.call_uint(config, address, "auction", signature, arguments, block, opts)

  defp call_words(config, address, signature, arguments, count, block, opts),
    do: LabRpc.call_words(config, address, "auction", signature, arguments, count, block, opts)

  defp current_step(envelope, step) do
    name = Atom.to_string(step)
    Enum.find(envelope["arguments"]["steps"], &(&1["step"] == name))
  end

  defp integer(envelope, key), do: envelope["arguments"][key] |> String.to_integer()
end
