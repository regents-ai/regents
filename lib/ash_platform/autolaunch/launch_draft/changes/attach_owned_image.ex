defmodule AshPlatform.Autolaunch.LaunchDraft.Changes.AttachOwnedImage do
  @moduledoc false
  use Ash.Resource.Change

  alias AshPlatform.Actors.Human
  alias AshPlatform.Autolaunch

  @impl true
  def change(changeset, _opts, %{actor: %Human{human_account_id: owner_id} = actor}) do
    image_id = Ash.Changeset.get_argument(changeset, :launch_draft_image_id)
    draft_id = changeset.data.id

    case Autolaunch.get_my_launch_draft_image(actor: actor) do
      {:ok, %{id: ^image_id, human_account_id: ^owner_id, launch_draft_id: ^draft_id} = image} ->
        url = AshPlatformWeb.Endpoint.url() <> "/autolaunch/images/#{image.id}/#{image.digest}"

        if String.valid?(url) and byte_size(url) <= 256 do
          changeset
          |> Ash.Changeset.change_attribute(:launch_draft_image_id, image.id)
          |> Ash.Changeset.change_attribute(:image, url)
        else
          unavailable(changeset)
        end

      _not_owned_by_this_draft ->
        unavailable(changeset)
    end
  end

  def change(changeset, _opts, _context), do: unavailable(changeset)

  defp unavailable(changeset) do
    Ash.Changeset.add_error(changeset,
      field: :launch_draft_image_id,
      message: "image is unavailable"
    )
  end
end
